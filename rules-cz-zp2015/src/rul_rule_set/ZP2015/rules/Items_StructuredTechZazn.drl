package ZP2015_StructuredTechZazn;

import cz.tacr.elza.domain.RulItemTypeExt;
import cz.tacr.elza.domain.RulItemType;
import cz.tacr.elza.domain.RulItemSpec;
import cz.tacr.elza.domain.RulItemSpecExt;
import cz.tacr.elza.drools.model.AvailableDescItems;
import cz.tacr.elza.drools.model.ActiveLevel;
import cz.tacr.elza.drools.model.Level;
import cz.tacr.elza.drools.model.DescItem;


// Orig. prvek: "ZP2015_ITEM_MAT"
// Detailní prvky popisu: 
//  "ZP2015_ITEM_MAT_TECHNIKA", "ZP2015_ITEM_MAT_ADJUSTACE", "ZP2015_ITEM_MAT_NOSIC",
//  "ZP2015_ITEM_MAT_LATKA_ZAZNAMU"

// Pravidla:
// Možný nebo doporuceny původní prvek -> možné nebo doporucene podrobné prvky

rule "mozny nebo doporuceny ZP2015_ITEM_MAT"
no-loop
when
  $itemTypeOld : RulItemTypeExt(code == "ZP2015_ITEM_MAT" && type in (RulItemType.Type.POSSIBLE, RulItemType.Type.RECOMMENDED))
  $itemType : RulItemTypeExt(code in ("ZP2015_ITEM_MAT_TECHNIKA", "ZP2015_ITEM_MAT_ADJUSTACE", 
              "ZP2015_ITEM_MAT_NOSIC",
              "ZP2015_ITEM_MAT_LATKA_ZAZNAMU") 
              )
then
  $itemType.setType($itemTypeOld.getType());
end


rule "povinny ZP2015_ITEM_MAT a neni vyplnen"
no-loop
when
  $itemTypeOld : RulItemTypeExt(code == "ZP2015_ITEM_MAT" && type==RulItemType.Type.REQUIRED)
  $itemType: RulItemTypeExt(code in ("ZP2015_ITEM_MAT_TECHNIKA", "ZP2015_ITEM_MAT_ADJUSTACE", 
                                     "ZP2015_ITEM_MAT_NOSIC", "ZP2015_ITEM_MAT_LATKA_ZAZNAMU") 
                                     )
  $activeLevel: ActiveLevel( )
  not (DescItem(type in ("ZP2015_ITEM_MAT", "ZP2015_ITEM_MAT_TECHNIKA", "ZP2015_ITEM_MAT_ADJUSTACE", 
                         "ZP2015_ITEM_MAT_NOSIC", "ZP2015_ITEM_MAT_LATKA_ZAZNAMU")                                      
                                      ) from $activeLevel.descItems
                                      )
then
  $itemType.setType(RulItemType.Type.REQUIRED);
end

rule "povinny ZP2015_ITEM_MAT a neco vyplneno"
no-loop
when
  $itemTypeOld : RulItemTypeExt(code == "ZP2015_ITEM_MAT" && type==RulItemType.Type.REQUIRED)
  $itemType: RulItemTypeExt(code in ("ZP2015_ITEM_MAT_TECHNIKA", "ZP2015_ITEM_MAT_ADJUSTACE", 
                                     "ZP2015_ITEM_MAT_NOSIC", "ZP2015_ITEM_MAT_LATKA_ZAZNAMU") )
  $activeLevel: ActiveLevel( )
  exists (DescItem(type in ("ZP2015_ITEM_MAT_TECHNIKA", "ZP2015_ITEM_MAT_ADJUSTACE", 
                                     "ZP2015_ITEM_MAT_NOSIC", "ZP2015_ITEM_MAT_LATKA_ZAZNAMU")
                                      ) from $activeLevel.descItems
                                      )
then
  $itemType.setType(RulItemType.Type.RECOMMENDED);
end

rule "snizeni povinnosti pro ZP2015_ITEM_MAT"
no-loop
salience -100
when
  $itemTypeOld : RulItemTypeExt(code == "ZP2015_ITEM_MAT" 
                                && type in (RulItemType.Type.RECOMMENDED, RulItemType.Type.REQUIRED)
                                )            
then
  $itemTypeOld.setType(RulItemType.Type.POSSIBLE);
end
