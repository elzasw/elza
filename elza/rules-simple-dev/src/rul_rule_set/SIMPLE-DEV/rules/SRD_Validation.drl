package SimpleDev;


import java.util.Arrays;
import cz.tacr.elza.drools.model.Level
import cz.tacr.elza.drools.model.ActiveLevel
import cz.tacr.elza.drools.model.DescItem;
import cz.tacr.elza.domain.vo.DataValidationResult;
import cz.tacr.elza.domain.vo.DataValidationResults;

global DataValidationResults results;

// Kontrola, zda každý list má ukládací jednotku
rule "List musí mít ukládací jednotku směrem ke kořeni"
when
	$activeLevel: ActiveLevel( !hasChildren )
	not ( $level: Level( $items: descItems ) and
		DescItem(type=="SRD_STORAGE_ID") from $items )
then
	results.createMissing("SRD_STORAGE_ID", "Chybí ukládací jednotka", "SRD_STORAGE_ID");
end

// Kontrola, zda každý list má dataci
rule "List musí mít dataci směrem ke kořeni"
when
	$activeLevel: ActiveLevel( !hasChildren )
	not ( $level: Level( $items: descItems ) and
		DescItem(type in
		  ( "SRD_UNIT_DATE", "SRD_UNIT_DATE_TEXT" ) )
		  from $items )
then
	results.createMissing("SRD_UNIT_DATE", "Není uvedena datace", "SRD_BASIC");
end

// Kontrola, zda každá hromadina s EJ má uvedenu ukládací jednotku s druhem KAR,FAS,DAJ
// TODO slapa: upravit na nové entity
/*rule "Každá hromadina s EJ má uvedenu ukládací jednotku"
when
	$activeLevel: ActiveLevel( $items: descItems )
	DescItem(type=="SRD_FOLDER_TYPE" && (specCode in ("SRD_FOLDER_UNITS", "SRD_FOLDER_SINGLE_TYPE") ) ) from $items
	not ( DescItem(type=="SRD_STORAGE_ID"&&packet!=null&&packet.packetType!=null&&
	                 (packet.packetType.code in ("SRD_BOX", "SRD_FASCICLE") ) ) from $items )
then
	results.createMissing("SRD_STORAGE_ID", "Ukládací jednotka bez uvedení typu hromadiny", "SRD_STORAGE_ID");
end*/

// uplatni se jen pro pomucky dle ZP
rule "Počet jednotek musí být větší než 1"
when
	$activeLevel: ActiveLevel( $items: descItems )
	DescItem(type=="SRD_ARRANGEMENT_TYPE" && specCode in ("SRD_ARRANGEMENT_MAN", "SRD_ARRANGEMENT_INV", "SRD_ARRANGEMENT_KAT") ) from $items
    $descItem: DescItem(type=="SRD_UNIT_COUNT"  && integer.intValue()<2 ) from $items
then
	results.createError($descItem.getDescItemId(), "Počet jednotek musí být větší než 1", "SRD_BASIC");
end

// Povinné pokud je nastaven typ pořádání
// výjimkou je kořen nebo hlavní série pokud mají nastaveno ref.označení
// Možné zlepšení: Kontrolovat, že žádný rodič nemá pořadové číslo, pokud ho nemá posuzovaná JP
rule "Pořadové číslo - povinné"
no-loop
when
	$activeLevel : ActiveLevel( $items: descItems )
	DescItem(type=="SRD_ARRANGEMENT_TYPE" && specCode in ("SRD_ARRANGEMENT_MAN", "SRD_ARRANGEMENT_INV", "SRD_ARRANGEMENT_KAT") ) from $items
	not DescItem(type=="SRD_SERIAL_NUMBER" && !isInherited()) from $items
	// Výjimka pro nejvyšší série a kořen
	not (
	  DescItem(type=="SRD_UNIT_ID" && !isInherited() ) from $items
	  and DescItem(type=="SRD_LEVEL_TYPE" && specCode in ("SRD_LEVEL_ROOT", "SRD_LEVEL_SECTION", "SRD_LEVEL_SERIES") ) from $items
	)
then
	results.createMissing("SRD_SERIAL_NUMBER",
	"Pořadové číslo musí být nastaveno (případně ref. označení pro nejvyšší úrovně popisu)",
	"SRD_POL_SERIAL_NO");
end

// referenční označení v INV|KAT - povinné pro uzly mimo kořen
rule "Referenční označení - povinné"
no-loop
when
	$activeLevel: ActiveLevel( parent!=null )
	DescItem(type=="SRD_ARRANGEMENT_TYPE" && specCode in ("SRD_ARRANGEMENT_INV", "SRD_ARRANGEMENT_KAT") ) from $activeLevel.descItems
	not DescItem(type=="SRD_UNIT_ID" && !isInherited()) from $activeLevel.descItems
then
	results.createMissing("SRD_UNIT_ID", "Referenční označení musí byt přiřazeno", "SRD_REF_NO");
end

// Kontrola, zda každý list má v hierarchii zdroj akvizice
rule "List musí mít zdroj akvizice směrem ke kořeni"
when
	$activeLevel: ActiveLevel( !hasChildren )
	DescItem(type=="SRD_ARRANGEMENT_TYPE" && specCode in ("SRD_ARRANGEMENT_MAN", "SRD_ARRANGEMENT_INV", "SRD_ARRANGEMENT_KAT") ) from $activeLevel.descItems
	not ( $level: Level( $items: descItems ) and 
		DescItem(type in 
		  ( "SRD_UNIT_SOURCE" ) )
		  from $items )
then
	results.createMissing("SRD_UNIT_SOURCE", "Není uveden zdroj akvizice", "SRD_BASIC");
end

// Kontrola, zda každý list má v hierarchii fyzický stav
rule "List musí mít popsán fyzický stav směrem ke kořeni"
when
	$activeLevel: ActiveLevel( !hasChildren )
	DescItem(type=="SRD_ARRANGEMENT_TYPE" && specCode in ("SRD_ARRANGEMENT_MAN", "SRD_ARRANGEMENT_INV", "SRD_ARRANGEMENT_KAT") ) from $activeLevel.descItems
	not ( $level: Level( $items: descItems ) and 
		DescItem(type in 
		  ( "SRD_UNIT_CURRENT_STATUS" ) )
		  from $items )
then
	results.createMissing("SRD_UNIT_CURRENT_STATUS",
	"Není uveden fyzický stav jednotky popisu a technické požadavky", "SRD_BASIC");
end

// Kontrola, zda každý list má zpracovatele
rule "List musí mít směrem ke kořeni alespoň jednoho zpracovatele"
when
	$activeLevel: ActiveLevel( !hasChildren )
	DescItem(type=="SRD_ARRANGEMENT_TYPE" && specCode in ("SRD_ARRANGEMENT_MAN", "SRD_ARRANGEMENT_INV", "SRD_ARRANGEMENT_KAT") ) from $activeLevel.descItems
	not ( $level: Level( $items: descItems ) and 
		DescItem(type in 
		  ( "SRD_ARRANGER" ) )
		  from $items )
then
	results.createMissing("SRD_ARRANGER", "Není uveden zpracovatel", "SRD_BASIC");
end

// Kontrola, zda každý list má určen jazyk
rule "List musí mít směrem ke kořeni alespoň jeden jazyk"
when
	$activeLevel: ActiveLevel( !hasChildren )
	DescItem(type=="SRD_ARRANGEMENT_TYPE" && specCode in ("SRD_ARRANGEMENT_MAN", "SRD_ARRANGEMENT_INV", "SRD_ARRANGEMENT_KAT") ) from $activeLevel.descItems
	not ( $level: Level( $items: descItems ) and 
		DescItem(type in 
		  ( "SRD_LANGUAGE" ) )
		  from $items )
then
	results.createMissing("SRD_LANGUAGE", "Není uveden jazyk", "SRD_BASIC");
end
