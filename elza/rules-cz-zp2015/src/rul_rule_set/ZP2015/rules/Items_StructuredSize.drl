package ZP2015_StructuredSize;

import cz.tacr.elza.domain.RulItemTypeExt;
import cz.tacr.elza.domain.RulItemType;
import cz.tacr.elza.domain.RulItemSpec;
import cz.tacr.elza.domain.RulItemSpecExt;
import cz.tacr.elza.drools.model.AvailableDescItems;
import cz.tacr.elza.drools.model.ActiveLevel;
import cz.tacr.elza.drools.model.Level;
import cz.tacr.elza.drools.model.DescItem;


// Orig. prvek: "ZP2015_SIZE"
// Detailní prvky popisu: 
//  "ZP2015_SIZE_WIDTH", "ZP2015_SIZE_HEIGHT", "ZP2015_SIZE_DEPTH",
//  "ZP2015_SIZE_UNITS", "ZP2015_WEIGHT", "ZP2015_AMOUNT"

// Pravidla:
// Možný původní prvek -> možné podrobné prvky
// Možný (doporučený, povinný) původní prvek -> možné specifikace pro nové prvky
// Doporučený původní prvek 
//     -> doporučené podrobné prvky
// Povinný původní prvek a prázdný a i nové prázdné 
//    -> povinné podrobné prvky
// Povinný původní prvek a prázdný a alespoň jeden nový neprázdný
//     -> doporučené podrobné prvky
//     -> původní možný 

rule "mozny ZP2015_SIZE"
no-loop
when
  RulItemTypeExt(code == "ZP2015_SIZE" && type==RulItemType.Type.POSSIBLE)
  $itemType : RulItemTypeExt(code in ("ZP2015_SIZE_WIDTH", "ZP2015_SIZE_HEIGHT", "ZP2015_SIZE_DEPTH",
              "ZP2015_SIZE_UNITS", "ZP2015_WEIGHT", "ZP2015_AMOUNT", "ZP2015_SIZE_OTHER") 
              )
then
  $itemType.setType(RulItemType.Type.POSSIBLE);
end

rule "mozne specifikace"
no-loop
when
  RulItemTypeExt(code == "ZP2015_SIZE" && type in (RulItemType.Type.POSSIBLE, RulItemType.Type.RECOMMENDED, RulItemType.Type.REQUIRED))
  $itemType : RulItemTypeExt(code in ("ZP2015_SIZE_UNITS", "ZP2015_WEIGHT", "ZP2015_AMOUNT") )
  $itemSpec : RulItemSpecExt( ) from $itemType.rulItemSpecList
then
  $itemSpec.setType(RulItemSpec.Type.POSSIBLE);
end

rule "doporuceny ZP2015_SIZE"
no-loop
when
  $itemTypeOld : RulItemTypeExt(code == "ZP2015_SIZE" && type==RulItemType.Type.RECOMMENDED)
  $itemType : RulItemTypeExt(code in ("ZP2015_SIZE_WIDTH", "ZP2015_SIZE_HEIGHT", "ZP2015_SIZE_DEPTH",
                                      "ZP2015_SIZE_UNITS", "ZP2015_WEIGHT", "ZP2015_AMOUNT", "ZP2015_SIZE_OTHER") 
              )
then
  $itemType.setType(RulItemType.Type.RECOMMENDED);
end

rule "povinny ZP2015_SIZE a neni vyplnen"
no-loop
when
  $itemTypeOld : RulItemTypeExt(code == "ZP2015_SIZE" && type==RulItemType.Type.REQUIRED)
  $itemType: RulItemTypeExt(code in ("ZP2015_SIZE_WIDTH", "ZP2015_SIZE_HEIGHT", "ZP2015_SIZE_DEPTH",
                                      "ZP2015_SIZE_UNITS", "ZP2015_WEIGHT", "ZP2015_AMOUNT", "ZP2015_SIZE_OTHER") )
  $activeLevel: ActiveLevel( )
  not (DescItem(type in ("ZP2015_SIZE", "ZP2015_SIZE_WIDTH", "ZP2015_SIZE_HEIGHT", "ZP2015_SIZE_DEPTH",
                                      "ZP2015_SIZE_UNITS", "ZP2015_WEIGHT", "ZP2015_AMOUNT", "ZP2015_SIZE_OTHER")                                      
                                      ) from $activeLevel.descItems
                                      )
then
  $itemType.setType(RulItemType.Type.REQUIRED);
end

rule "povinny ZP2015_SIZE a neco vyplneno"
no-loop
when
  $itemTypeOld : RulItemTypeExt(code == "ZP2015_SIZE" && type==RulItemType.Type.REQUIRED)
  $itemType: RulItemTypeExt(code in ("ZP2015_SIZE_WIDTH", "ZP2015_SIZE_HEIGHT", "ZP2015_SIZE_DEPTH",
                                      "ZP2015_SIZE_UNITS", "ZP2015_WEIGHT", "ZP2015_AMOUNT", "ZP2015_SIZE_OTHER") )
  $activeLevel: ActiveLevel( )
  exists (DescItem(type in ("ZP2015_SIZE", "ZP2015_SIZE_WIDTH", "ZP2015_SIZE_HEIGHT", "ZP2015_SIZE_DEPTH",
                                      "ZP2015_SIZE_UNITS", "ZP2015_WEIGHT", "ZP2015_AMOUNT", "ZP2015_SIZE_OTHER")                                      
                                      ) from $activeLevel.descItems
                                      )
then
  $itemType.setType(RulItemType.Type.RECOMMENDED);
end

rule "Jine rozmery - průměr - pečetidla, razítka, pečeti, odlitky, otisky typáře, kopie otisku, mapy (glóby), štočky, předměty numizmatické povahy, předměty faleristické povahy, jiné - možné"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_PEC", "ZP2015_UNIT_TYPE_RAZ", "ZP2015_UNIT_TYPE_OTD", "ZP2015_UNIT_TYPE_OTC", "ZP2015_UNIT_TYPE_MAP", "ZP2015_UNIT_TYPE_STO", "ZP2015_UNIT_TYPE_PNP", "ZP2015_UNIT_TYPE_PFP", "ZP2015_UNIT_TYPE_JIN") ) from $ownItems
    $itemType: RulItemTypeExt( code in ( "ZP2015_SIZE_OTHER" ) )
    $itemSpec: RulItemSpecExt( code in (
        // průměr
        "ZP2015_SIZE_DIAMETER",
        // obrazové pole (průměr)
        "ZP2015_SIZE_IMAGE_DIAMETER"
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.POSSIBLE);
end

rule "Jine rozmery - průměr - pečetidla, razítka, pečeti, odlitky, otisky typáře, kopie otisku, mapy (glóby), štočky, předměty numizmatické povahy, předměty faleristické povahy, jiné - doporucene"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $itemTypeOld : RulItemTypeExt(code == "ZP2015_SIZE" && type==RulItemType.Type.REQUIRED)
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_PEC", "ZP2015_UNIT_TYPE_RAZ", "ZP2015_UNIT_TYPE_OTD", "ZP2015_UNIT_TYPE_OTC", "ZP2015_UNIT_TYPE_MAP", "ZP2015_UNIT_TYPE_STO", "ZP2015_UNIT_TYPE_PNP", "ZP2015_UNIT_TYPE_PFP", "ZP2015_UNIT_TYPE_JIN") ) from $ownItems
    $itemType: RulItemTypeExt( code in ( "ZP2015_SIZE_OTHER" ) )
    $itemSpec: RulItemSpecExt( code in (
        // průměr
        "ZP2015_SIZE_DIAMETER",
        // obrazové pole (průměr)
        "ZP2015_SIZE_IMAGE_DIAMETER"
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.RECOMMENDED);
end

rule "Jine rozmery - plika - listiny - možné"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_LIO", "ZP2015_UNIT_TYPE_LIP") ) from $ownItems 
    $itemType: RulItemTypeExt( code in ("ZP2015_SIZE_OTHER") )
    $itemSpec: RulItemSpecExt( code in (
        // plika
        "ZP2015_SIZE_PLICA"        
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.POSSIBLE);
end

rule "Jine rozmery - plika - listiny - doporucene"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $itemTypeOld : RulItemTypeExt(code == "ZP2015_SIZE" && type==RulItemType.Type.REQUIRED)
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_LIO", "ZP2015_UNIT_TYPE_LIP") ) from $ownItems 
    $itemType: RulItemTypeExt( code in ("ZP2015_SIZE_OTHER") )
    $itemSpec: RulItemSpecExt( code in (
        // plika
        "ZP2015_SIZE_PLICA"        
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.RECOMMENDED);
end

rule "Jine rozmery - zrcadlo - mapy a atlasy - možné"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_MAP", "ZP2015_UNIT_TYPE_ATL") ) from $ownItems
    $itemType: RulItemTypeExt( code in ( "ZP2015_SIZE_OTHER" ) )
    $itemSpec: RulItemSpecExt( code in (
        // zrcadlo (šířka)
        "ZP2015_SIZE_MAP_WIDTH",
        // zrcadlo (výška)
        "ZP2015_SIZE_MAP_HEIGHT"
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.POSSIBLE);
end

rule "Jine rozmery - zrcadlo - mapy a atlasy - doporucene"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $itemTypeOld : RulItemTypeExt(code == "ZP2015_SIZE" && type==RulItemType.Type.REQUIRED)
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_MAP", "ZP2015_UNIT_TYPE_ATL") ) from $ownItems
    $itemType: RulItemTypeExt( code in ( "ZP2015_SIZE_OTHER" ) )
    $itemSpec: RulItemSpecExt( code in (
        // zrcadlo (šířka)
        "ZP2015_SIZE_MAP_WIDTH",
        // zrcadlo (výška)
        "ZP2015_SIZE_MAP_HEIGHT"
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.RECOMMENDED);
end

rule "Jine rozmery - obrazové pole - technické výkresy, grafické listy, kresby, fotografické archiválie, kinematografické záznamy, pohlednice, plakáty, cenné papíry, štočky, jiné - možné"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_TVY", "ZP2015_UNIT_TYPE_GLI", "ZP2015_UNIT_TYPE_KRE", "ZP2015_UNIT_TYPE_FSN", "ZP2015_UNIT_TYPE_FSD", "ZP2015_UNIT_TYPE_LFI", "ZP2015_UNIT_TYPE_SFI", "ZP2015_UNIT_TYPE_KIN", "ZP2015_UNIT_TYPE_MF", "ZP2015_UNIT_TYPE_MFS", "ZP2015_UNIT_TYPE_KZA", "ZP2015_UNIT_TYPE_POH", "ZP2015_UNIT_TYPE_PKT", "ZP2015_UNIT_TYPE_CPA", "ZP2015_UNIT_TYPE_STO", "ZP2015_UNIT_TYPE_JIN") ) from $ownItems
    $itemType: RulItemTypeExt( code in ( "ZP2015_SIZE_OTHER" ) )
    $itemSpec: RulItemSpecExt( code in (
        // obrazové pole (šířka)
        "ZP2015_SIZE_IMAGE_WIDTH",
        // obrazové pole (výška)
        "ZP2015_SIZE_IMAGE_HEIGHT"
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.POSSIBLE);
end

rule "Jine rozmery - obrazové pole - technické výkresy, grafické listy, kresby, fotografické archiválie, kinematografické záznamy, pohlednice, plakáty, cenné papíry, štočky, jiné - doporucene"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $itemTypeOld : RulItemTypeExt(code == "ZP2015_SIZE" && type==RulItemType.Type.REQUIRED)
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_TVY", "ZP2015_UNIT_TYPE_GLI", "ZP2015_UNIT_TYPE_KRE", "ZP2015_UNIT_TYPE_FSN", "ZP2015_UNIT_TYPE_FSD", "ZP2015_UNIT_TYPE_LFI", "ZP2015_UNIT_TYPE_SFI", "ZP2015_UNIT_TYPE_KIN", "ZP2015_UNIT_TYPE_MF", "ZP2015_UNIT_TYPE_MFS", "ZP2015_UNIT_TYPE_KZA", "ZP2015_UNIT_TYPE_POH", "ZP2015_UNIT_TYPE_PKT", "ZP2015_UNIT_TYPE_CPA", "ZP2015_UNIT_TYPE_STO", "ZP2015_UNIT_TYPE_JIN") ) from $ownItems
    $itemType: RulItemTypeExt( code in ( "ZP2015_SIZE_OTHER" ) )
    $itemSpec: RulItemSpecExt( code in (
        // obrazové pole (šířka)
        "ZP2015_SIZE_IMAGE_WIDTH",
        // obrazové pole (výška)
        "ZP2015_SIZE_IMAGE_HEIGHT"
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.RECOMMENDED);
end

rule "Jine rozmery - metráž - svitkové filmy, kinofilmy, mikrofilmy, kinematografické záznamy, zvukové záznamy - možné"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_SFI", "ZP2015_UNIT_TYPE_KIN", "ZP2015_UNIT_TYPE_MF", "ZP2015_UNIT_TYPE_KZA", "ZP2015_UNIT_TYPE_ZZA") ) from $ownItems
    $itemType: RulItemTypeExt( code in ( "ZP2015_SIZE_OTHER" ) )
    $itemSpec: RulItemSpecExt( code in (
        // metráž
        "ZP2015_SIZE_METRAGE"
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.POSSIBLE);
end

rule "Jine rozmery - metráž - svitkové filmy, kinofilmy, mikrofilmy, kinematografické záznamy, zvukové záznamy - doporucene"
no-loop
when $activeLevel: ActiveLevel( $ownItems: ownDescItems )
    $itemTypeOld : RulItemTypeExt(code == "ZP2015_SIZE" && type==RulItemType.Type.REQUIRED)
    $descItem : DescItem(type=="ZP2015_UNIT_TYPE" && specCode in ("ZP2015_UNIT_TYPE_SFI", "ZP2015_UNIT_TYPE_KIN", "ZP2015_UNIT_TYPE_MF", "ZP2015_UNIT_TYPE_KZA", "ZP2015_UNIT_TYPE_ZZA") ) from $ownItems
    $itemType: RulItemTypeExt( code in ( "ZP2015_SIZE_OTHER" ) )
    $itemSpec: RulItemSpecExt( code in (
        // metráž
        "ZP2015_SIZE_METRAGE"
     ) ) from $itemType.rulItemSpecList
then
    $itemSpec.setMinType(RulItemSpec.Type.RECOMMENDED);
end

rule "snizeni povinnosti pro ZP2015_SIZE"
no-loop
salience -100
when
  $itemTypeOld : RulItemTypeExt(code == "ZP2015_SIZE" 
                                && type in (RulItemType.Type.RECOMMENDED, RulItemType.Type.REQUIRED)
                                )            
then
  $itemTypeOld.setType(RulItemType.Type.POSSIBLE);
end

rule "změna povinnosti PP Jiné rozměry podle nastavení specifikace"
no-loop
salience -300
when 
    $itemType: RulItemTypeExt( code in ( "ZP2015_SIZE_OTHER" ) )
then
    $itemType.setTypeMaxFromSpecs();
end