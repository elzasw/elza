package ZP2015;

import cz.tacr.elza.domain.RulItemSpec;
import cz.tacr.elza.domain.RulItemTypeExt;
import cz.tacr.elza.domain.RulItemSpecExt;
import cz.tacr.elza.drools.model.AvailableDescItems;
import cz.tacr.elza.drools.model.ActiveLevel;
import cz.tacr.elza.drools.model.Level;
import cz.tacr.elza.drools.model.DescItem;

import java.util.Arrays;
import java.util.Set;
import cz.tacr.elza.domain.RulItemType;

rule "Vypnutí opakovatelnosti"
salience 100
no-loop
when
  $itemType : RulItemTypeExt(code in ( // nastaveni pro ulozeni
        "ZP2015_PACKET_TYPE", "ZP2015_PACKET_FIXED_PREFIX", "ZP2015_PACKET_PREFIX",
        "ZP2015_PACKET_NUMBER", "ZP2015_PACKET_POSTFIX", "ZP2015_PACKET_METRAGE"))
then
  $itemType.setRepeatable(false);
end

rule "Možné prvky popisu"
no-loop
when
    $itemType : RulItemTypeExt(code in ("ZP2015_PACKET_PREFIX", "ZP2015_PACKET_POSTFIX", 
                                        "ZP2015_PACKET_METRAGE"))
then
    $itemType.setType(RulItemType.Type.POSSIBLE);
end

rule "Možné typy obalů"
no-loop
when
    $itemType : RulItemTypeExt(code == "ZP2015_PACKET_TYPE")
    $itemSpec : RulItemSpecExt(code in ("ZP2015_PACKET_TYPE_SAMOST",
                                        "ZP2015_PACKET_TYPE_KAR",
                                        "ZP2015_PACKET_TYPE_FAS")) from $itemType.rulItemSpecList
then
    $itemSpec.setType(RulItemSpec.Type.POSSIBLE);
end

rule "Možné typy prefixů"
no-loop
when
    $itemType : RulItemTypeExt(code == "ZP2015_PACKET_FIXED_PREFIX")
    $itemSpec : RulItemSpecExt() from $itemType.rulItemSpecList
then
    $itemSpec.setType(RulItemSpec.Type.POSSIBLE);
end

rule "Doporučené"
no-loop
when
    $itemType : RulItemTypeExt(code in ("ZP2015_PACKET_NUMBER"))
then
    $itemType.setType(RulItemType.Type.RECOMMENDED);
end

rule "Povinné"
no-loop
when
    $itemType : RulItemTypeExt(code in ("ZP2015_PACKET_TYPE"))
then
    $itemType.setType(RulItemType.Type.REQUIRED);
end
